# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromecast_build.gni")

declare_args() {
  # Select platforms automatically. Turn this off for manual control.
  ozone_auto_platforms = true

  # This enables memory-mapped access to accelerated graphics buffers via the
  # VGEM ("virtual GEM") driver. This is currently only available on Chrome OS
  # kernels and affects code in the GBM ozone platform.
  # TODO(dshwang): remove this flag when all gbm hardware supports vgem map.
  # crbug.com/519587
  use_vgem_map = false
}

declare_args() {
  # The platform that will be active by default.
  ozone_platform = ""

  # Enable individual platforms.
  ozone_platform_caca = false
  ozone_platform_cast = false
  ozone_platform_egltest = false
  ozone_platform_gbm = false
  ozone_platform_ozonex = false
  ozone_platform_headless = false

  if (ozone_auto_platforms) {
    if (is_chromecast) {
      ozone_platform_headless = true
      if (disable_display) {
        # Audio-only builds should use the "headless" platform by default.
        ozone_platform = "headless"
      } else {
        # Builds with video should default to the "cast" platform.
        ozone_platform_cast = true
        ozone_platform = "cast"

        # For desktop Chromecast builds, override the default "cast" platform with
        # --ozone_platform=egltest
        if (target_os == "linux" && target_cpu != "arm") {
          ozone_platform_egltest = true
          ozone_platform_ozonex = true
        }
      }
    } else {
      # Use headless as the default platform.
      ozone_platform = "headless"

      # Build all platforms whose deps are in install-build-deps.sh.
      # Only these platforms will be compile tested by buildbots.
      ozone_platform_gbm = true
      ozone_platform_headless = true
      ozone_platform_egltest = true
    }
  }
}
